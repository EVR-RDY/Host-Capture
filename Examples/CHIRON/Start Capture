:: Modified 22JAN21

@ECHO OFF
title CHIRON V2.0
setlocal enabledelayedexpansion

cd /D %~dp0




echo.
echo  ________  ___  ___  ___  ________  ________  ________      
echo ^|\   ____\^|\  \^|\  \^|\  \^|\   __  \^|\   __  \^|\   ___  \    
echo \ \  \___^|\ \  \\\  \ \  \ \  \^|\  \ \  \^|\  \ \  \\ \  \ 
echo  \ \  \    \ \   __  \ \  \ \   _  _\ \  \\\  \ \  \\ \  \ 
echo   \ \  \____\ \  \ \  \ \  \ \  \\  \\ \  \\\  \ \  \\ \  \
echo    \ \_______\ \__\ \__\ \__\ \__\\ _\\ \_______\ \__\\ \__\
echo     \^|_______^|\^|__^|\^|__^|\^|__^|\^|__^|\^|__^|\^|_______^|\^|__^| \^|__^| v2.0
echo.
::----[ This code block detects if the script is being running with admin PRIVILEGES If it isn't it pauses and then quits]-------
NET SESSION >nul 2>&1
IF %ERRORLEVEL% EQU 0 (
	echo ... Admin privileges detected^^!
) ELSE (
   echo ################ ERROR^: ADMIN PRIVILEGES REQUIRED^^! #################   
   echo         This script must be run with Admin privileges^^!
   echo       Right click the .bat file and "Run As Administrator".
   echo ####################################################################### 
   echo.
   pause
   EXIT /B 1
)



:COLLECTION
echo.
echo *************************************************************************
echo                            COLLECTION
echo *************************************************************************
echo Where are you running CHIRON?
echo 1 - External Harddrive
echo 2 - Network
echo 3 - CDROM/DVD
echo.
echo.
SET M=1
::SET /P M= Select your choice, then press ENTER: 
echo *************************************************************************

IF %M%==1 GOTO setcollectext
IF %M%==2 GOTO setcollectnet
IF %M%==3 GOTO setcollectcd

echo Invalid Choice^^!
GOTO COLLECTION



:setcollectext
set external=%~d0
set home=%~dp0
set toolpath=%home%scripts
set binaries=%home%binaries
set collection=%home%Collection
goto finish

:setcollectnet
set home=pushd %~dp0
set toolpath=%home%scripts
set binaries=%home%binaries
set collection=%home%Collection

:setcollectcd 
set external=%~d0
set home=%~dp0
set toolpath=%home%scripts
set binaries=%home%binaries
set collection=c:\Collect
%binaries%\mkdir.exe -p %collection% >nul 2>&1
goto finish

:finish
set KAP=%home%KAPE
set m=%DATE:~4,2%
set d=%DATE:~7,2%
set y=%DATE:~10,4%
set hh=%TIME:~0,2%
set mm=%TIME:~3,2%
if "%hh:~0,1%"==" " (set hh=0%hh:~1,1%)
set timestamp=%y%.%m%.%d%.%hh%.%mm%.
set outpath=%collection%\%computername%--%timestamp%
%binaries%\mkdir.exe -p %outpath% >nul 2>&1

:: ************************* Establish Logging *************************

set logfile="%outpath%\%computername%-%timestamp%.log"
echo %DATE% %TIME%: Logging: %logfile%  | %binaries%\tee.exe %logfile%
echo %DATE% %TIME%: Running as %USERNAME% on %computername%...  | %binaries%\tee.exe -a %logfile%
echo.  | %binaries%\tee.exe -a %logfile% >nul 2>&1
echo ******************************************************************** >> %logfile%

::echo external=%external% > %toolpath%\path.txt 
echo home=%home%> %toolpath%\path.txt
echo toolpath=%toolpath%>> %toolpath%\path.txt
echo binaries=%binaries%>> %toolpath%\path.txt
echo collection=%collection%>> %toolpath%\path.txt
echo KAP=%KAP%>> %toolpath%\path.txt
echo timestamp=%timestamp%>> %toolpath%\path.txt
echo outpath=%outpath%>> %toolpath%\path.txt
echo logfile=%logfile%>> %toolpath%\path.txt

@REM echo $drive = (get-location).drive.name
@REM echo $tools = (get-location).path
@REM echo $kape = "${tools}\kape\kape.exe"
echo %outpath%> %toolpath%\kape.txt


:promptmem
echo.
echo.
echo *************************************************************************
echo                           MEMORY COLLECTION
echo *************************************************************************
echo Would you like to conduct a memory capture?
echo 1 - YES
echo 2 - NO
ECHO.
echo.
:prompt
::SET M=2
SET /P M= Select your choice, then press ENTER: 
echo *************************************************************************
echo %DATE% %TIME%:MEMORY COLLECTION option %M% >> %logfile%
IF %M%==1 GOTO memcap
IF %M%==2 GOTO promptart

echo Invalid Choice^^!
GOTO promptmem

:memcap
echo.
call %toolpath%\MemCap.bat /B 1
GOTO promptart

:promptart
echo.
echo.
echo ************************************************************************* 
echo                   ARTIFACT COLLECTION                              
echo ************************************************************************* 
echo.
echo.
echo Would you like to conduct a artifact capture?
echo 1 - YES
echo 2 - NO
ECHO.
echo.
::SET M=1
SET /P M= Select your choice, then press ENTER: 
echo *************************************************************************
echo %DATE% %TIME%:ARTIFACT COLLECTION option %M% >> %logfile%
IF %M%==1 GOTO arttype
IF %M%==2 GOTO opnotes
echo Invalid Choice^^!
GOTO promptart

:arttype
echo.
echo.
echo *************************************************************************
echo                           ARTIFACT COLLECTION TYPE
echo *************************************************************************
echo.
for %%i in (powershell.exe) do (
	if "%%~$path:i"=="" (
		set pshell=no
		echo .         No PowerShell was detected please choose LEGACY MODE
	) else (
		set pshell=yes
		echo PowerShell detected Please choose NORMAL MODE
	)
)

echo.
echo What type of artifact capture would you like?
echo.
echo.
echo 1 - NORMAL MODE: Run Script off the Ext. HD ^& Save Output to Ext. HD
echo 2 - LEGACY MODE: Force run LEGACY
echo 3 - CRITICAL MODE: Run Light weight Version with no Memory Capture

::SET M=2
SET /P M= Select your choice, then press ENTER:
echo *************************************************************************
echo %DATE% %TIME%:ARTIFACT COLLECTION TYPE option %M% >> %logfile%
IF %M%==1 GOTO NORMAL
IF %M%==2 GOTO LEGACY
IF %M%==3 GOTO CRITICAL
echo Invalid Choice^^!
GOTO arttype

:NORMAL
echo %DATE% %TIME%: Start Kape >> %logfile%
powershell.exe -executionpolicy bypass "%toolpath%\kape2.ps1" /B 1
echo %DATE% %TIME%: Finish Kape >> %logfile%
echo %DATE% %TIME%: Start Opnotes >> %logfile%
powershell.exe -executionpolicy bypass "%toolpath%\Opnotes.ps1" /B 1
echo %DATE% %TIME%: Finish Opnotes >> %logfile%
rename %outpath% "%computername%--%timestamp%--CHIRON"
GOTO exit

:LEGACY
echo %DATE% %TIME%: Start LEGACYCOMMON.bat >> %logfile%
call %toolpath%\LEGACYCOMMON.bat /B 1
echo %DATE% %TIME%: Finish LEGACYCOMMON.bat >> %logfile%
rename %outpath% "%computername%--%timestamp%--LEGACY"
GOTO opnotes

:CRITICAL
echo %DATE% %TIME%: Start LEGACYCOMMON.bat >> %logfile%
call %toolpath%\LEGACYCOMMON.bat /B 1
echo %DATE% %TIME%: Finish LEGACYCOMMON.bat >> %logfile%
echo %DATE% %TIME%: Start CRITICAL.bat >> %logfile%
call %toolpath%\CRITICAL.bat /B 1
echo %DATE% %TIME%: Start CRITICAL.bat >> %logfile%
rename %outpath% "%computername%--%timestamp%--CRITICAL"
GOTO opnotes



:opnotes
echo %DATE% %TIME%: Start Opnotes >> %logfile%
echo.
echo. >> %collection%\Opnotes.csv

set done=1

IF %done%==1 (
	systeminfo > systeminfo.txt
	echo.
	set /p date="Today's Date (eg. 01Jan2020): "
	set /p start="Enter Collection Start Time (REAL time; eg. 2130): "
	set /p finish="Enter Collection Finish Time (REAL time; eg. 2200): "
	set /p building="Enter Building and Room Number (eg. Bldg123 Rm123): "
	set /p location="Enter Physical Location of system (eg. "under desk"): "
	set /p purpose="Enter System Purpose (eg. Server/HMI/Production Monitoring): "
	set /p harddrive="Enter Collection Hard Drive (eg. Seagate6/S6): "
	set /p analyst="Enter Analyst Name: "
	set /p success="Was Collection Successful? (y/n) "
	set /p notes="Enter other notes (eg. chemical produced/second capture/etc.): "
	set network= 
	
) ELSE (
	set /p exit="Press ENTER to exit..."
	exit
)

type systeminfo.txt | findstr /L /B /c:"Host Name" > info.txt
for /f "usebackq tokens=2* delims=:" %%b in (`type info.txt`) do (set hostname=%%b)
type systeminfo.txt | findstr /L /B /c:"OS Name" > info.txt
for /f "usebackq tokens=2* delims=:" %%b in (`type info.txt`) do (set os=%%b)
type systeminfo.txt | findstr /L /B /c:"OS Version" > info.txt
for /f "usebackq tokens=2* delims=:" %%b in (`type info.txt`) do (set build=%%b)

for /F "usebackq tokens=*" %%a in (`ipconfig /all`) do (
	echo %%a | find "Description" >> temp.txt
	echo %%a | find "IP Address" >> temp.txt
	echo %%a | find "Physical Address" >> temp.txt
)

for /F "tokens=2* delims=:" %%a in (temp.txt) do (
	echo %%a >> network.txt
)

echo. >> %collection%\Opnotes.csv
echo %date%,%hostname%,%network%,%os%,%build%,%building%,%location%,%purpose%,%start%,%finish%,%harddrive%,%analyst%,%success%,%notes% >> %collection%\Opnotes.csv
type network.txt >> %collection%\Opnotes.csv


del %home%systeminfo.txt>nul 2>&1
del %home%info.txt >nul 2>&1
del %home%temp.txt >nul 2>&1
del %home%network.txt >nul 2>&1


GOTO exit

:exit
echo.
set /p leave=Press ENTER to quit...
exit



